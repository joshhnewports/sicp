(define (make-new-machine)
  (let ((pc (make-register 'pc))
        (flag (make-register 'flag))
        (stack (make-stack))
        (the-instruction-sequence '())
	(instructions-executed 0)
	(trace? false)
	(breakpoints '())
	(halted? false)) ;prevent breakpoints from permanently halting machine
    (let ((the-ops
           (list (list 'initialize-stack
                       (lambda () (stack 'initialize)))))
          (register-table
           (list (list 'pc pc) (list 'flag flag))))
      (define (allocate-register name)
        (if (assoc name register-table)
            (error "Multiply defined register: " name)
            (set! register-table
                  (cons (list name (make-register name))
                        register-table)))
        'register-allocated)
      (define (lookup-register name)
        (let ((val (assoc name register-table)))
          (if val
              (cadr val)
              (error "Unknown register:" name))))
      (define (trace inst)
	(if (not (null? (instruction-label inst)))
	    (begin (newline)
		   (display (instruction-label inst))))
	(newline)
	(display (instruction-text inst)))
      (define (halt inst breakpoint)
	(newline)
	(display (list 'halting-machine (breakpoint-label breakpoint) (breakpoint-offset breakpoint))))
      (define (execute)
        (let ((insts (get-contents pc)))
          (if (null? insts)
              'done
              (let ((inst (car insts)))
		(if trace?
		    (trace inst))
		(let ((breakpoint (get-breakpoint inst breakpoints)))
		  (if (and breakpoint (not halted?))
		      (begin (set! halted? true)
			     (halt inst breakpoint))
		      (begin (set! halted? false)
			     (set! instructions-executed (+ instructions-executed 1))
			     ((instruction-execution-proc (car insts)))
			     (execute))))))))
      (define (dispatch message)
        (cond ((eq? message 'start)
	       (set! instructions-executed 0)
               (set-contents! pc the-instruction-sequence)
               (execute))
              ((eq? message 'install-instruction-sequence)
               (lambda (seq)
		 (let ((index 1))
		   (for-each (lambda (inst)
			       (set-instruction-index! inst index)
			       (set! index (+ index 1)))
			     seq)
		 (set! the-instruction-sequence seq)))) 
              ((eq? message 'allocate-register) allocate-register)
              ((eq? message 'get-register) lookup-register)
              ((eq? message 'install-operations)
               (lambda (ops) (set! the-ops (append the-ops ops))))
              ((eq? message 'stack) stack)
              ((eq? message 'operations) the-ops)
	      ((eq? message 'instructions-executed) instructions-executed)
	      ((eq? message 'trace-on) (set! trace? true) 'trace-on)
	      ((eq? message 'trace-off) (set! trace? false) 'trace-off)
	      ((eq? message 'trace-on-reg)
	       (lambda (reg-name) ((lookup-register reg-name) 'trace-on)))
	      ((eq? message 'trace-off-reg)
	       (lambda (reg-name) ((lookup-register reg-name) 'trace-off)))
	      ((eq? message 'set-breakpoint)
	       (lambda (label offset)
		 (let ((new-breakpoint (make-breakpoint label offset)))
		   (set-breakpoint-index!
		    new-breakpoint
		    (search-breakpoint-index new-breakpoint the-instruction-sequence))
		   (set! breakpoints (cons new-breakpoint breakpoints)))
		 'breakpoint-set))
	      ((eq? message 'cancel-breakpoint)
	       (lambda (label offset)
		 (set! breakpoints (remove-breakpoint (make-breakpoint label offset) breakpoints))
		 'breakpoint-removed))
	      ((eq? message 'cancel-all-breakpoints) (set! breakpoints '()) 'breakpoints-canceled)
	      ((eq? message 'proceed) (execute))
              (else (error "Unknown request -- MACHINE" message))))
      dispatch)))

(define (make-breakpoint label offset)
  (list label offset '()))
(define (breakpoint-label breakpoint)
  (car breakpoint))
(define (breakpoint-offset breakpoint)
  (cadr breakpoint))
(define (breakpoint-index breakpoint)
  (caddr breakpoint))
(define (set-breakpoint-index! breakpoint index)
  (set-car! (cddr breakpoint) index))

(define (get-breakpoint inst breakpoints)
  (cond ((null? breakpoints) false)
	((eq? (instruction-index inst) (breakpoint-index (car breakpoints)))
	 (car breakpoints))
	(else (get-breakpoint inst (cdr breakpoints)))))

;;breakpoint is not is not identitical to the one in breakpoints, breakpoint will not have an index
(define (remove-breakpoint breakpoint breakpoints)
  (filter (lambda (bp)
	    (not (and (eq? (breakpoint-label breakpoint)
			   (breakpoint-label bp))
		      (eq? (breakpoint-offset breakpoint)
			   (breakpoint-offset bp)))))
	  breakpoints))

;;broken into two intuitive procedures rather than some terrible if/cond
(define (search-breakpoint-index breakpoint the-instruction-sequence)
  (define (search-for-label label inst-seq)
    (cond ((null? inst-seq)
	   (error "Unknown instruction -- SET-BREAKPOINT" label (breakpoint-offset breakpoint)))
	  ((eq? label (instruction-label (car the-instruction-sequence)))
	   (search-for-offset (breakpoint-offset breakpoint) inst-seq))
	  (else (search-for-label label (cdr inst-seq)))))
  (define (search-for-offset offset inst-seq)
    (cond ((null? inst-seq)
	   (error "Unknown instruction -- SET-BREAKPOINT" (breakpoint-label breakpoint) offset))
	  ((= offset 1) (instruction-index (car inst-seq)))
	  (else (search-for-offset (- offset 1) (cdr inst-seq)))))
  (search-for-label (breakpoint-label breakpoint) the-instruction-sequence))

(define (make-instruction text)
  (list text '() '() '()))
(define (instruction-index inst)
  (cadddr inst))
(define (set-instruction-index! inst index)
  (set-car! (cdddr inst) index))

(define (set-breakpoint machine label n)
  ((machine 'set-breakpoint) label n))
(define (cancel-breakpoint machine label n)
  ((machine 'cancel-breakpoint) label n))
(define (cancel-all-breakpoints machine)
  (machine 'cancel-all-breakpoints))
(define (proceed-machine machine)
  (machine 'proceed))
